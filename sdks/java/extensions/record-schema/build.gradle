plugins { id 'org.apache.beam.module' }
applyJavaNature(
        automaticModuleName: 'org.apache.beam.sdk.extensions.recordschema',
        publish: true,
        exportJavadoc: true,
        enableSpotbugs: false,
)

dependencies {
    implementation project(path: ":sdks:java:core")
    implementation library.java.vendored_guava_32_1_2_jre

    testImplementation library.java.junit
    testImplementation library.java.joda_time
    testImplementation project(path: ":sdks:java:core", configuration: "shadowTest")
}

project.tasks.withType(JavaCompile.class).configureEach {
    def releaseIndex = options.compilerArgs.findIndexOf {it == "--release"}
    if (releaseIndex != -1) {
        options.compilerArgs.remove(releaseIndex)
        options.compilerArgs.remove(releaseIndex)
    }
}

// checkstyle doesn't understand the record syntax, we need to disable it in test sources
tasks.named("checkstyleTest", Checkstyle.class) {
    exclude("**/RecordSchemaTest.java")
    exclude("**/TestRecords.java")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

    sourceCompatibility = "17"
    targetCompatibility = "17"
}
